apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'coveralls'
apply plugin: 'idea'

version = '0.1'
project.ext['rootPackage'] = 'net.ihiroky.niotty'
project.ext['encoding'] = 'UTF-8'

// jar.baseName = 'niotty'
sourceCompatibility = 1.6
targetCompatibility = 1.6
[compileJava, compileTestJava].each {
    it.options.encoding = encoding
}
compileJava.options.compilerArgs = ['-Xlint:all']

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.2.5'
    }
}

repositories {
    mavenCentral()
}

configurations {
    doclet
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'net.java.dev.jna:jna:4.0.0'
    runtime 'ch.qos.logback:logback-classic:1.0.13'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    doclet 'org.umlgraph:umlgraph:5.6'
}

checkstyleMain.configFile = file('config/checkstyle/main.xml')
checkstyleTest.configFile = file('config/checkstyle/test.xml')

tasks.withType(FindBugs) {
    excludeFilter = file('config/findbugs/exclude.xml')
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true  // for coveralls plugin
        html.enabled = true
    }
}

// Override the javadoc task to use umlgraph doclet.  This depends on the presence
// of GraphViz on the path in your environment (http://www.graphviz.org)
javadoc {
    description 'Generates Javadoc API documentation with apiviz diagrams'
    options.windowTitle = "Uds4j API Reference (${version})"
    options.encoding = encoding
    options.locale = 'en_US'
    options.docletpath = configurations.doclet.files.asType(List)
    options.doclet = 'org.umlgraph.doclet.UmlGraphDoc'
    options.addStringOption 'verbose'
    options.addStringOption 'inferrel'
    options.addStringOption 'inferdep'
    options.addStringOption 'inferdepvis', 'protected'
    options.addStringOption 'qualify'
    options.addStringOption 'postfixpackage'
    options.addStringOption 'hide', '(java.*|org.*)'
    options.addStringOption 'collpackages', 'java.util.*'
    options.addStringOption 'nodefontsize', '9'
    options.addStringOption 'nodefontpackagesize', '7'
    options.addStringOption 'link', 'http://docs.oracle.com/javase/7/docs/jdk/api/javadoc/doclet/'
    options.addStringOption 'link', 'http://docs.oracle.com/javase/7/docs/api/'
}

